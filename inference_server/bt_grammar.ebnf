// BehaviorTree XML Grammar
// Based on W3C XML 1.0 specification, adapted for Lark EBNF syntax

start: xml_decl root

xml_decl: "<?xml" WS "version" WS? "=" WS? version_val "?>"
version_val: "\"1.0\""

root: "<root" " " "BTCPP_format=\"4\"" " " main_tree_attr ((" " | "\t") attribute)* " "? ">" WS? behavior_tree WS? "</root>"
main_tree_attr: "main_tree_to_execute=\"" att_value_content "\""

behavior_tree: "<BehaviorTree" ((" " | "\t") attribute)* " "? ">" WS? bt_content WS? "</BehaviorTree>"

// Root level: can be a single action OR a control node
bt_content: action_node | root_control

// Control nodes for complex behaviors
root_control: simple_sequence | simple_fallback | simple_parallel

// Simple control nodes contain only actions (no nested control nodes)
simple_sequence: "<Sequence" ((" " | "\t") attribute)* " "? ">" WS? action_list WS? "</Sequence>"
simple_fallback: "<Fallback" ((" " | "\t") attribute)* " "? ">" WS? action_list WS? "</Fallback>"
simple_parallel: "<Parallel" ((" " | "\t") attribute)* " "? ">" WS? action_list WS? "</Parallel>"

// List of actions (no control nodes allowed here)
action_list: action_node (WS? action_node)*

// Action nodes (always self-closing)
// Spin listed first to reduce bias, then others alphabetically
action_node: spin | drive_on_heading | backup | detect_obj | pick_obj | place_obj | wait | compute_path | follow_path

// Action nodes with mandatory and optional attributes (no duplicates allowed)
// Format: required attributes first, then optional ones in fixed order
compute_path: "<ComputePathToPose" " " goal_attr " " path_attr (" " planner_id_attr)? (" " name_attr)? " "? "/>"
follow_path: "<FollowPath" " " path_attr (" " controller_id_attr)? (" " name_attr)? " "? "/>"
spin: "<Spin" " " spin_dist_attr " " time_allowance_attr (" " name_attr)? " "? "/>"
drive_on_heading: "<DriveOnHeading" " " dist_to_travel_attr " " speed_attr " " time_allowance_attr (" " name_attr)? " "? "/>"
backup: "<BackUp" " " backup_dist_attr " " backup_speed_attr " " time_allowance_attr (" " name_attr)? " "? "/>"
wait: "<Wait" " " wait_duration_attr (" " name_attr)? " "? "/>"
detect_obj: "<DetectObject" " " object_description_attr " " detect_target_pose_attr " " detect_object_pose_attr (" " name_attr)? " "? "/>"
pick_obj: "<PickObject" " " pick_target_pose_attr (" " name_attr)? " "? "/>"
place_obj: "<PlaceObject" " " place_target_pose_attr (" " name_attr)? " "? "/>"

// Specific parameter definitions
goal_attr: "goal" " "? "=" " "? "\"" goal_var "\""
path_attr: "path" " "? "=" " "? "\"" path_var "\""
planner_id_attr: "planner_id" " "? "=" " "? "\"" att_value_content "\""
controller_id_attr: "controller_id" " "? "=" " "? "\"" att_value_content "\""
spin_dist_attr: "spin_dist" " "? "=" " "? "\"" "-"? numeric_value "\""
dist_to_travel_attr: "dist_to_travel" " "? "=" " "? "\"" numeric_value "\""
speed_attr: "speed" " "? "=" " "? "\"" numeric_value "\""
backup_dist_attr: "backup_dist" " "? "=" " "? "\"" numeric_value "\""
backup_speed_attr: "backup_speed" " "? "=" " "? "\"" numeric_value "\""
time_allowance_attr: "time_allowance" " "? "=" " "? "\"" numeric_value "\""
wait_duration_attr: "wait_duration" " "? "=" " "? "\"" numeric_value "\""
object_description_attr: "object_description" " "? "=" " "? "\"" att_value_content "\""
detect_target_pose_attr: "target_pose" " "? "=" " "? "\"{goal}\""
detect_object_pose_attr: "object_pose" " "? "=" " "? "\"{object_pose}\""
pick_target_pose_attr: "target_pose" " "? "=" " "? "\"{object_pose}\""
place_target_pose_attr: "target_pose" " "? "=" " "? "\"{object_pose}\""
name_attr: "name" " "? "=" " "? "\"" att_value_content "\""

// Variable name constraints - force specific variable names
goal_var: "{goal}"
path_var: "{path}"

numeric_value: /[0-9]+\.?[0-9]*/
att_value_content: /[^<&";]*/

// Generic attribute for control nodes and root elements
attribute: att_name " "? "=" " "? "\"" att_value_content "\""
att_name: /[a-zA-Z_][a-zA-Z0-9_]*/

WS: /[ \t\n\r]+/
